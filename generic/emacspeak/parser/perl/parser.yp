#
# Copyright (C) 2017 Mike Ray <mike.ray@btinternet.com>
#
# Parse::Yapp grammar/parser/lexer for the Emacspeak text-to-speech protocol.
#
# This is free software; you can redistribute it 		and/or modify it
# under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2, or (at your option)
# any later version.
#
# This software is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this package; see the file COPYING.  If not, write to
# the Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor,
# Boston, MA 02110-1301, USA.
#
#--code--


%{

# These constants are used as start states, a-la %x in Yacc.
use constant X_INITIAL			=> 0;

# q or tts_say
use constant X_SPEECH			=> 1;

# a (play sound file)
use constant X_RESOURCE			=> 2;

# l (single letter)
use constant X_LETTER			=> 4;

# c
use constant X_CODE				=> 8;

# we're inside braces
use constant X_INBRACES			=> 16;

# All free-form payloads.
# Payloads for these three functions don't conform to fixed format, like integer, punctuation levels, flag etc. FF = free-form
use constant X_FF			=> (X_SPEECH|X_LETTER|X_CODE);

our $VERSION = '1.0.0';

# set to a non-zero value to see debug messages
my $debug = 0;

%}


#%token INTEGER

%%

# Rules section

cmdlist
	: 
	| cmdlist cmd
	;

cmd
	: code { $_[1]; }
	| speech { $_[1]; }
	| dispatch
	  {
			&{$_[0]->YYData->{SA}->{tts_d}};
					$_[1];
						}
	| stop
	  {
			&{$_[0]->YYData->{SA}->{tts_s}};
		$_[1];
		}
	| version
		{
			&{$_[0]->YYData->{SA}->{tts_version}};
			$_[1];
		}
	| silence
		{ 
			&{$_[0]->YYData->{SA}->{tts_silence}}($_[1]);
$_[1];
		}
	| beep { $_[1]; }
	| tone { $_[1]; }
	| sound { $_[1]; }
	| tts_pause
		{
			&{$_[0]->YYData->{SA}->{tts_pause}};
			$_[1];
		}
	| tts_reset
		{
			&{$_[0]->YYData->{SA}->{tts_reset}};
			$_[1];
		}
	| tts_resume
		{
			&{$_[0]->YYData->{SA}->{tts_resume}};
			$_[1];
		}
	| tts_allcaps_beep
		{
			&{$_[0]->YYData->{SA}->{tts_allcaps_beep}}($_[1]);
			$_[1];
		}
	| tts_capitalize
		{
			&{$_[0]->YYData->{SA}->{tts_capitalize}}($_[1]);
			$_[1];
		}
	| tts_set_character_scale
		{
			$_[1];
		}
	| tts_set_punctuations
		{
			&{$_[0]->YYData->{SA}->{tts_set_punctuations}}($_[1]);
			$_[1];
		}
	| tts_set_speech_rate
		{ 
			&{$_[0]->YYData->{SA}->{tts_set_speech_rate}}($_[1]);
				$_[1];		
		}
	| tts_split_caps
		{
			&{$_[0]->YYData->{SA}->{tts_split_caps}}($_[1]);
			$_[1];
		}
	| tts_sync_state
		{
			$_[1];
		}
	;

code
	: 'c' '{' CODE '}' '\n'
		{
			&{$_[0]->YYData->{SA}->{tts_c}}($_[3]);
			$_[1];
		}
	| 'c' ATOM '\n'
		{
			&{$_[0]->YYData->{SA}->{tts_c}}($_[2]);
			$_[1];
		}
	;

speech
	: immediate_speech { $_[1]; }
	| queued_speech { $_[1]; }
	| character { $_[1]; }
	;

immediate_speech
	: TTS_SAY '{' TEXT '}' '\n'
		{
			&{$_[0]->YYData->{SA}->{tts_say}}($_[3]);
			$_[1];
		}
	| TTS_SAY ATOM '\n'
		{
			&{$_[0]->YYData->{SA}->{tts_say}}($_[2]);
			$_[1];
		}
	;

character
	: 'l' '{' CHAR '}' '\n'
		{
			&{$_[0]->YYData->{SA}->{tts_l}}($_[3]);
			$_[1];
		}
	| 'l' CHAR '\n'
		{
			&{$_[0]->YYData->{SA}->{tts_l}}($_[2]);
			$_[1];
		}
	;

queued_speech
	: 'q' '{' TEXT '}' '\n'
		{
			&{$_[0]->YYData->{SA}->{tts_q}}($_[3]);
			$_[1];
		}
	| 'q' ATOM '\n'
		{
			&{$_[0]->YYData->{SA}->{tts_q}}($_[2]);
			$_[1];
		}
	;

dispatch
	: 'd' '\n' { $_[1]; }
	;

version
	: VERSION '\n' { $_[1]; }
	;

stop
	: 's' '\n' { $_[1]; }
	;

silence
	: SH '{' INTEGER '}' '\n'
		{
			$_[3];
		}
	| SH INTEGER '\n'
		{
			$_[2];
		}
	;

beep
	: 'b' '{' INTEGER INTEGER '}' '\n'
		{
			&{$_[0]->YYData->{SA}->{tts_b}}($_[3], $_[4]);
			$_[1];
		}
	| 'b' INTEGER INTEGER '\n'
		{
			&{$_[0]->YYData->{SA}->{tts_b}}($_[2], $_[3]);
			$_[1];
		}
	;

tone
	: 't' '{' INTEGER INTEGER '}' '\n'
		{
			&{$_[0]->YYData->{SA}->{tts_t}}($_[3], $_[4]);
			$_[1];
		}
	| 't' INTEGER INTEGER '\n'
		{
			&{$_[0]->YYData->{SA}->{tts_t}}($_[2], $_[3]);
			$_[1];
		}
	;

sound
	: 'a' '{' RESOURCE '}' '\n'
		{
			&{$_[0]->YYData->{SA}->{tts_a}}($_[3]);
			$_[1];
		}
	| 'a' RESOURCE '\n'
		{
			&{$_[0]->YYData->{SA}->{tts_a}}($_[2]);
			$_[1];
		}
	;

tts_pause
	: TTS_PAUSE '\n' { $_[1]; }
	;

tts_reset
	: TTS_RESET '\n' { $_[1]; }
	;

tts_resume
	: TTS_RESUME '\n' { $_[1]; }
	;

tts_allcaps_beep
	: TTS_ALLCAPS_BEEP '{' FLAG '}' '\n' { $_[3]; }
	| TTS_ALLCAPS_BEEP FLAG '\n' { $_[2]; }
	;

tts_capitalize
	: TTS_CAPITALIZE '{' FLAG '}' '\n' { $_[3]; }
	| TTS_CAPITALIZE FLAG '\n' { $_[2]; }
	;

tts_set_character_scale
	: TTS_SET_CHARACTER_SCALE '{' DOUBLE '}' '\n'
		{
			&{$_[0]->YYData->{SA}->{tts_set_character_scale}}($_[3]);
			$_[1];
		}
	| TTS_SET_CHARACTER_SCALE DOUBLE '\n'
		{
			&{$_[0]->YYData->{SA}->{tts_set_character_scale}}($_[2]);
			$_[1];
		}
	;

tts_set_punctuations
	: TTS_SET_PUNCTUATIONS '{' punctlevel '}' '\n' { $_[3]; }
	| TTS_SET_PUNCTUATIONS punctlevel '\n' { $_[2]; }
	;

tts_set_speech_rate
	: TTS_SET_SPEECH_RATE '{' INTEGER '}' '\n' { $_[3]; }
	| TTS_SET_SPEECH_RATE INTEGER '\n' { $_[2]; }
	;

tts_split_caps
	: TTS_SPLIT_CAPS '{' FLAG '}' '\n' { $_[3]; }
	| TTS_SPLIT_CAPS FLAG '\n' { $_[2]; }
	;

tts_sync_state
	: TTS_SYNC_STATE '{' punctlevel FLAG FLAG FLAG INTEGER '}' '\n'
		{ 
			&{$_[0]->YYData->{SA}->{tts_sync_state}}($_[3], $_[4], $_[5], $_[6], $_[7]);
			$_[1];
		}
	| TTS_SYNC_STATE punctlevel FLAG FLAG FLAG INTEGER '\n'
		{
			&{$_[0]->YYData->{SA}->{tts_sync_state}}($_[2],$_[3], $_[4], $_[5], $_[6]);
			$_[1];
		}
	;

punctlevel
	: NONE
	| SOME
	| ALL
	;

%%

# Bottom section

sub _Error {
	exists $_[0]->YYData->{ERRMSG}
		and do {
			print $_[0]->YYData->{ERRMSG};
			delete $_[0]->YYData->{ERRMSG};
			return;
	};
    
	print "Syntax error.\n";

} # sub _Error

sub _Lexer {
	my($parser)=shift;

	$parser->YYData->{INPUT}
		or  $parser->YYData->{INPUT} = <STDIN>
		or  return('',undef);

		# don't want to consume whitespace if we're inside braces in the c function
		unless ( ($parser->YYData->{STATE} & X_CODE ) &&
($parser->YYData->{STATE} & X_INBRACES ) ) {
	# Eat white space
    $parser->YYData->{INPUT} =~ s/^[ \t]+//;
}

	# This is not a loop. It is just here to set the $_ variable
	for ($parser->YYData->{INPUT}) {

		# Parse text if we're inside braces for c
		if ( ($parser->YYData->{STATE} & X_CODE ) &&
($parser->YYData->{STATE} & X_INBRACES ) ) {
			print "State X_CODE/X_INBRACES\n" if $debug;
			s/^([^}]+)//
			and do {
				$parser->YYData->{STATE} = X_INITIAL;
				print "About to return code: >$1<\n" if $debug;
				return('CODE',$1);
			};
		}


		# Parse text if we're inside braces for q and tts_say
		if ( ($parser->YYData->{STATE} & X_SPEECH ) &&
($parser->YYData->{STATE} & X_INBRACES ) ) {
			print "State X_SPEECH/X_INBRACES\n" if $debug;
			s/^\s*([^}]+)\s*//
			and do {
				$parser->YYData->{STATE} = X_INITIAL;
				print "About to return text: >$1<\n" if $debug;
				return('TEXT',$1);
			};
		}

		
		# Parse atom (string with no spaces) for q and tts_say
		if ($parser->YYData->{STATE} & X_SPEECH ) {
			s/^([^ {}\n]+)//
			and do {
				print "About to return ATOM\n" if $debug;
				$parser->YYData->{STATE} = X_INITIAL;
				return('ATOM',$1);
			};
		}

		# Parse resource (string with no spaces) for a
		if ($parser->YYData->{STATE} & X_RESOURCE ) {
			s/^([^ {}\n]+)//
			and do {
				print "About to return RESOURCE\n" if $debug;
				$parser->YYData->{STATE} = X_INITIAL;
				return('RESOURCE',$1);
			};
		}
		
		# tts_say, immediate speech. Set state to X_SPEECH and return function token
		s/^(tts_say)//i
		and do {
			$parser->YYData->{STATE} |= X_SPEECH;
			return('TTS_SAY',$1);
		};

		# Parse single character (letter) for the l function
		if ( ($parser->YYData->{STATE} & X_LETTER ) &&
($parser->YYData->{STATE} & X_INBRACES ) ) {
			print "State X_LETTER/X_INBRACES\n" if $debug;
			s/^\s*([^}])\s*//
			and do {
				$parser->YYData->{STATE} = X_INITIAL;
				print "About to return letter: >$1<\n" if $debug;
				return('CHAR',$1);
			};
		}

		# Parse single letter 
		if ($parser->YYData->{STATE} & X_LETTER ) {
			s/^([^ {}\n])//
			and do {
				print "About to return CHAR\n" if $debug;
				$parser->YYData->{STATE} = X_INITIAL;
				return('CHAR',$1);
			};
		}

		# Version, no arguments
		s/^(version)//i
		and return('VERSION',$1);

		# punctuation levels
		s/^((none|some|all))//
		and do {
			return(uc($1),{'none'=>0,'some'=>1,'all'=>2}->{$1});
		};

		# Pause, reset and resume have no arguments

		# pause
		s/^(tts_pause)//i
		and return('TTS_PAUSE',$1);

		# reset 
		s/^(tts_reset)//i
		and return('TTS_RESET',$1);

		# resume 
		s/^(tts_resume)//i
		and return('TTS_RESUME',$1);

		# These tts_* functions either have braces or not

		# tts_allcaps_beep
		s/^(tts_allcaps_beep)//i
		and return('TTS_ALLCAPS_BEEP',$1);

		# tts_capitalize
		s/^(tts_capitalize)//i
		and return('TTS_CAPITALIZE',$1);

		# tts_set_character_scale
		s/^(tts_set_character_scale)//i
		and return('TTS_SET_CHARACTER_SCALE',$1);

		# tts_set_punctuations	
		s/^(tts_set_punctuations)//i
		and return('TTS_SET_PUNCTUATIONS',$1);

		# tts_set_speech_rate
		s/^(tts_set_speech_rate)//i
		and do {
			$parser->YYData->{CONDITION} = 1;
			return('TTS_SET_SPEECH_RATE',$1);
		};

		# tts_split_caps
		s/^(tts_split_caps)//i
		and return('TTS_SPLIT_CAPS',$1);

		# tts_sync_state with or without braces but I've never seen braces

		# tts_sync_state
		s/^(tts_sync_state)//i
		and return('TTS_SYNC_STATE',$1);

	# silence 'sh'
		s/^(sh)//i
		and return('SH',$1);

		# Start of single-character patterns

		# Dispatch 'd'
		s/^(d)//i
		and return($1,$1);

		# stop 's'
		s/^(s)//i
			and return($1,$1);

		# code 'c'
		s/^(c)//i
		and do {
			$parser->YYData->{STATE} |= X_CODE;
			return($1,$1);
		};

		# single letter 'l'
		s/^(l)//i
		and do {
			print "Found single-letter prefix\n" if $debug;
			$parser->YYData->{STATE} |= X_LETTER;
			return(lc($1),$1);
		};


		# queued speech 'q'
		s/^(q)//i
		and do {
			print "Found q\n" if $debug;
			$parser->YYData->{STATE} |= X_SPEECH;
			return(lc($1),$1);
		};

		# tone 't' 
		s/^(t)//i
		and return($1,$1);

		# beep 'b'
		s/^(b)//i
		and return($1,$1);

		# sound file 'a' or 'p'
		s/^((a|p))//i
		and do {
			$parser->YYData->{STATE} |= X_RESOURCE;
			return($1,$1);
		};

		# numeric patterns

		# integer
		s/^([0-9][0-9]+)//
		and return('INTEGER',$1);

		# double
		s/^([-+]?([0-9]*\.[0-9]+|[0-9]+\.))//
			and return('DOUBLE',$1);

		# flag 0 or 1
		s/^([01])//
		and return('FLAG',$1);

		# Left-brace
		s/^({)//
		and do {
			if ($parser->YYData->{STATE} & X_FF) {
				print "Found left brace while in X_FF\n" if $debug;
				$parser->YYData->{STATE} |= X_INBRACES;
			}
			return($1,$1);
		};

		# Right-brace
		s/^(})//
		and do {
			print "Found right brace\n" if $debug;
			$parser->YYData->{STATE} = X_INITIAL;
			return($1,$1);
		};

		# newline
		s/^(\n)//s
		and do {
			print "Found newline\n" if $debug;
			$parser->YYData->{STATE} = X_INITIAL;
			return($1,$1);
		};

	} # end of for()

} # end of _Lexer

sub Run {
	my($self)=shift;
	$self->YYParse( yylex => \&_Lexer, yyerror => \&_Error );
} # end of Run

1;


__END__

=pod

=head1 name

Parse::Emacspeak - Emacspeak text-to-speech protocol parser

=head1 SYNOPSIS

	use Parse::Emacspeak;

	my $parser = new Emacspeak;

	# Preset state to zero
	$parser->YYData->{STATE} = 0;

	my $value = $parser->YYParse(
			yylex => \&Emacspeak::_Lexer,
			yyerror => \&Emacspeak::_Error,
			yydebug => shift || 0
		);

	my $nberr = $parser->YYNberr();

	$parser->Run;

=head1 EXAMPLE

	#!/usr/bin/perl -w

	use strict;
	use warnings;

	use Parse::Emacspeak;

	my $parser = new Emacspeak;

	# Preset state to zero
	$parser->YYData->{STATE} = 0;

	# Load references to semantic action subroutines
	$parser->YYData->{SA}->{tts_d} = \&tts_d;
	$parser->YYData->{SA}->{tts_s} = \&tts_s;
	$parser->YYData->{SA}->{tts_version} = \&tts_version;
	$parser->YYData->{SA}->{tts_silence} = \&tts_silence;
	$parser->YYData->{SA}->{tts_pause} = \&tts_pause;
	$parser->YYData->{SA}->{tts_reset} = \&tts_reset;
	$parser->YYData->{SA}->{tts_resume} = \&tts_resume;
	$parser->YYData->{SA}->{tts_allcaps_beep} = \&tts_allcaps_beep;
	$parser->YYData->{SA}->{tts_capitalize} = \&tts_capitalize;
	$parser->YYData->{SA}->{tts_set_punctuations} = \&tts_set_punctuations;
	$parser->YYData->{SA}->{tts_set_speech_rate} = \&tts_set_speech_rate;
	$parser->YYData->{SA}->{tts_split_caps} = \&tts_split_caps;
	$parser->YYData->{SA}->{tts_c} = \&tts_c;
	$parser->YYData->{SA}->{tts_say} = \&tts_say;
	$parser->YYData->{SA}->{tts_l} = \&tts_l;
	$parser->YYData->{SA}->{tts_q} = \&tts_q;
	$parser->YYData->{SA}->{tts_b} = \&tts_b;
	$parser->YYData->{SA}->{tts_t} = \&tts_t;
	$parser->YYData->{SA}->{tts_a} = \&tts_a;
	$parser->YYData->{SA}->{tts_set_character_scale} = \&tts_set_character_scale;
	$parser->YYData->{SA}->{tts_sync_state} = \&tts_sync_state;

	my $value = $parser->YYParse(
			yylex => \&Emacspeak::_Lexer,
			yyerror => \&Emacspeak::_Error,
			yydebug => shift || 0
		);

	my $nberr = $parser->YYNberr();

	$parser->Run;

	#-- Semantic action subroutines

	# These subs are called by the semantic actions in the parser.
	# See the load of semantic actions above.
	#
	# They are called in the parser like this, for example to call tts_say:
	#
	# &{$_[0]->YYData->{SA}->{tts_say}}($_[n]);
	#
 
	sub tts_d() {
		print "Called tts_d\n";
	}

	sub tts_s() {
		print "Called tts_s\n";
	}

	sub tts_version() {
		print "Called tts_version\n";
	}

	sub tts_silence() {
		my ($duration) = @_;
		print "Called tts_silence: >$duration<\n";
	}

	sub tts_pause() {
		print "Called tts_pause\n";
	}

	sub tts_reset() {
		print "Called tts_reset\n";
	}

	sub tts_resume() {
		print "Called tts_resume\n";
	}

	sub tts_allcaps_beep() {
		my ($flag) = @_;
		print "Called tts_allcaps_beep: >$flag<\n";
	}

	sub tts_capitalize() {
		my ($flag) = @_;
		print "Called tts_capitalize: >$flag<\n";
	}

	sub tts_set_punctuations() {
		my ($punctuations) = @_;
		print "Called tts_set_punctuations: >$punctuations<\n";
	}

	sub tts_set_speech_rate() {
		my ($rate) = @_;
		print "Called tts_set_speech_rate: >$rate<\n";
	}

	sub tts_split_caps() {
		my ($flag) = @_;
		print "Called tts_split_caps: >$flag<\n";
	}

	sub tts_c() {
		my ($code) = @_;
		print "Called tts_c: >$code<\n";
	}

	sub tts_say() {
		my ($speech) = @_;
		print "Called tts_say: >$speech<\n";
	}

	sub tts_l() {
		my ($letter) = @_;
		print "Called tts_l: >$letter<\n";
	}

	sub tts_q() {
		my ($speech) = @_;
		print "Called tts_q: >$speech<\n";
	}

	sub tts_b() {
		my ($pitch, $duration) = @_;
		print "Called tts_b: >$pitch<, >$duration<\n";
	}

	sub tts_t() {
		my ($pitch, $duration) = @_;
		print "Called tts_t: >$pitch<, >$duration<\n";
	}


	sub tts_a() {
		my ($sound) = @_;
		print "Called tts_a: >$sound<\n";
	}

	sub tts_set_character_scale() {
		my ($scale) = @_;
		print "Called tts_set_character_scale: >$scale<\n";
	}

	sub tts_sync_state() {
		my ($punc,$capitalize, $allcapsbeep,$splitcaps,$rate) = @_;
		print "Called tts_sync_state: $punc, $capitalize, $allcapsbeep,$splitcaps,$rate\n";
	}

=head1 DESCRIPTION

Emacspeak is an extension to emacs written by T.V. Raman.

It communicates with a speech-synthesiser via a protocol written to STDOUT with text-to-speech 
functions which resemble TCL function calls. Current text-to-speech servers bundled with Emacspeak 
are TCL scripts.

This module uses Parse::Yapp to parse the protocol by reading STDIN, tokenising the text and calling 
appropriate tts functions.

=head2 SEMANTIC ACTIONS

See the manual for Parse::Yapp for an explanation of the syntax of semantic actions in the 
Parse::Yapp grammar.

In every rule the first array entry ($_[0]) contains the parser object.

Text-to-speech functions to be called when a rule is reduced must be loaded into the parser object 
within the user-data hash:

	$parser->YYData

This module uses:

	$_[0]->YYData->{SA}->{name-of-tts-function-to-call}

For example to call the function called when an Emacspeak dispatch function is called (letter d 
followed by newline, and nothing else on a line), assuming you have called the function 'tts_d' in 
your code:

	$parser->YYData->{SA}-{ttsd} = \&tts_d;

It's then called in the semantic action like this:

	&{$_[0]->YYData->{SA}->{tts_d}};

And a function with arguments, for example 'tts_set_speech_rate' assuming the speech rate is $_[2]:

	&{$_[0]->YYData->{SA}->{tts_set_speech_rate}}($_[3]);

See the above example of a test script.

=head1 IMPORTANT

There *MUST* be a subroutine reference in the parser object for every rule in the grammar, even if 
the speech-synthesiser you are writing code for does not support the functionality represented by 
that function.

